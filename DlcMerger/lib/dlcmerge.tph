INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

DEFINE_ACTION_FUNCTION MERGE_DLC
INT_VAR
  fail_on_error = 1
STR_VAR
  dlc_res     = ~sod-dlc~
  path_temp   = ~~
  path_unzip  = ~unzip~
RET
  success   // success status
  message   // error message on failure
BEGIN
  OUTER_SET success = 0
  OUTER_TEXT_SPRINT message ~~

  OUTER_SET failed = 0

  ACTION_IF (~%path_temp%~ STR_EQ ~~) BEGIN
    OUTER_TEXT_SPRINT path_temp ~%MOD_FOLDER%/temp~
  END

  LAF GET_DLC_INFO
    STR_VAR dlc_res
    RET dlc_filespec dlc_directory dlc_ext
  END

  // Doing quick DLC integrity check
  LAF IS_DLC_VALID
  STR_VAR
    path_dlc = EVAL ~%dlc_filespec%~
    path_unzip
  RET
    success
  END
  ACTION_IF (NOT success) BEGIN
    ACTION_IF (fail_on_error) BEGIN
      FAIL @200 // DLC archive is corrupt or incomplete.
    END ELSE BEGIN
      OUTER_SET failed = 1
      OUTER_SPRINT message @200 // DLC archive is corrupt or incomplete.
    END
  END

  ACTION_IF (NOT failed) BEGIN
    PRINT @101  // Unpacking DLC archive. This may take a while...
    MKDIR ~%path_temp%~
    AT_NOW ret_val ~%unzip% -oqq "%dlc_filespec%" -x mod.key -d "%path_temp%"~
    ACTION_IF (ret_val != 0) BEGIN
      DELETE + ~%path_temp%~
      ACTION_IF (fail_on_error) BEGIN
        FAIL @202 // Could not unpack DLC archive.
      END ELSE BEGIN
        OUTER_SET failed = 1
        OUTER_SPRINT message @202 // Could not unpack DLC archive.
      END
    END
  END

  ACTION_IF (NOT failed) BEGIN
    LAF COPY_RECURSIVE
    INT_VAR
      delete = 1  // delete file after copy operation
    STR_VAR
      source_dir  = EVAL ~%path_temp%~
      dest_dir    = ~~
      biff_folder = EVAL ~%dlc_res%~
    END

    PRINT @100  // Merging keys...
    AT_NOW ret_val ~%unzip% -oqq "%dlc_filespec%" mod.key -d "%path_temp%"~
    ACTION_IF (ret_val != 0 || NOT FILE_EXISTS ~%path_temp%/mod.key~) BEGIN
      DELETE + ~%path_temp%~
      ACTION_IF (fail_on_error) BEGIN
        FAIL @200 // DLC archive is corrupt or incomplete.
      END ELSE BEGIN
        OUTER_SET failed = 1
        OUTER_SPRINT message @200 // DLC archive is corrupt or incomplete.
      END
    END

    ACTION_IF (NOT failed) BEGIN
      LAF MERGE_KEYS
      STR_VAR
        chitin_key  = ~chitin.key~
        mod_key     = EVAL ~%path_temp%/mod.key~
        biff_folder = EVAL ~%dlc_res%~
      RET
        merge_success = success
      END
      ACTION_IF (NOT merge_success) BEGIN
        DELETE + ~%path_temp%~
        ACTION_IF (fail_on_error) BEGIN
          FAIL @201 // Unable to merge keys.
        END ELSE BEGIN
          OUTER_SET failed = 1
          OUTER_SPRINT message @201 // Unable to merge keys.
        END
      END
    END
  END

  ACTION_IF (NOT failed) BEGIN
    DELETE + ~%path_temp%~
    // TODO: MOVE currently fails if paths contain spaces
    ACTION_IF (~%dlc_filespec%~ STRING_CONTAINS_REGEXP ~[ %TAB%]~ = 0) BEGIN
      PRINT ~IMPORTANT: Please rename "%dlc_filespec%" to "%dlc_res%.disabled".~
    END ELSE BEGIN
      MOVE ~%dlc_filespec%~ ~%dlc_directory%/%dlc_res%.disabled~
    END
    OUTER_SET success = 1
  END
END
